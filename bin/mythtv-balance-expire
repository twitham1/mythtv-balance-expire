#!/usr/bin/perl -w

# mythtv-balance-expire by twitham@sbcglobal.net, 2017-02-18

# perldoc this file for full manual, or just search for __END__ below.

# All disks must have expiring recordings, ideally from the full range
# of recorded time so that each disk expires only oldest recordings.
# This distributes expirable recordings evenly over all disks and
# time.  If a move makes a disk fuller than the last, then a trade
# with a keeper is done to keep similar disk headroom.  Assumptions:
# all files are local.  Tested with 2 disks but should work with many.

use warnings;
use MythTV;
use Getopt::Long;
use Pod::Usage;

my %opt = (delta => 5);	       # lower = better balance but more moves
GetOptions(\%opt, qw(help|? man delta=i subtitles verbose go keepers=i))
    or pod2usage(2);
pod2usage(1) if $opt{help};
pod2usage(-verbose => 2) if $opt{man};

# How to copy the files.  If successful, originals are unlinked
my @copy = qw/ionice -n 7 nice rsync -a/; # must exit 0 on success only
push @copy, '--progress' if -t STDIN;
my $RECENT = time - 3 * 24 * 60 * 60; # don't move recent files
my $MAX = 0.5;			# maximum free space to use for copies
my $MIN = 2.0;			# minimum disk headroom in GB
my $KB = 1024;
my $MB = $KB * $KB;
my $GB = $MB * $KB;
my @exp;			# ordered expiring basenames
my %exp;			# {dir}{expiring basenames}
my %keep;			# {dir}{keeping basenames}
my %size;			# {basename} = bytes in file
my %title;			# {basename} = title [/ subtitle]
my %where;			# {basename} = dir
my %free;			# {dir} = bytes free on disk
my %count = qw/< 0 > 0 GB< 0 GB> 0/; # {type} = count/size of moves

my $myth = new MythTV();   # we'll keep expires in chronological order:
my %rows = $myth->backend_rows('QUERY_RECORDINGS Ascending');

# UNDOCUMENTED: ignore DB and pretend X % are keepers (for dev/testing)
defined $opt{keepers} and $opt{go} = 0;

foreach my $row (@{$rows{rows}}) { # gather status and location of shows
    my $show = $myth->new_recording(@$row);
    my $key = $show->{basename};
    my $path = $show->{local_path};
    (my $dir = $path) =~ s@/[^/]+$@@;
    $dir =~ m@/@ or next;	# only old files in a local path
    $show->{lastmodified} < $RECENT or next;
    $size{$key} = $show->{filesize};
    $title{$key} = $show->{title};
    $opt{subtitles} and $title{$key} .= " / $show->{subtitle}";
    $where{$key} = $dir;
    defined $opt{keepers} and  # UNDOCUMENTED: pretend X % are keepers
	$show->{auto_expire} = rand(100) > $opt{keepers} ? 1 : 0;
    $show->{auto_expire} ? push @exp, $key : $keep{$dir}{$key}++;
    $exp{$dir} or $exp{$dir} = { one => 1 };
}

my @path = sort keys %exp;	# storage paths found
@path > 1 or die "need 2 or more paths to balance, found: @path\n";
my $n = 0;
for my $dir (@path) {		# per-disk metrics
    $n{$dir} = $n++;		# disk number, for indent columns
    $free{$dir} = &free($dir) * $KB; # bytes free, updated by &move
}

while (my $move = shift @exp) {	# expirables in time order
    my @p = sort { keys %{$exp{$a}} <=> keys %{$exp{$b}} } @path;
    my $more = pop @p;		# path with most expires
    my $less = shift @p;	# path with least expires
    my $exp = scalar keys %{$exp{$where{$move}}};
    print "\t" x $n{$where{$move}}, "$exp\t$move\t$where{$move}"
	. ($moved{$move} ? " # moved \@ $moved{$move}\n" : "\n");
    unless ($where{$move} eq $more) { # keep it here if not path of most
	$exp{$where{$move}}{$move}++;
	next;
    }
    if ($moved{$move} or	# move files only once
	(keys %{$exp{$more}}) - (keys %{$exp{$less}}) < $opt{delta}) {
	$exp{$more}{$move}++;	# balanced, keep it here
	next;
    }
    # don't move to fuller disk unless full disk has larger trade
    my $large = (sort { $size{$a} <=> $size{$b} }
		 grep { !$moved{$_} } keys %{$keep{$less}},
		 grep { $where{$_} eq $less } @exp)[-1];
    if ($free{$less} < $free{$more} and
	(!$large or $size{$large} < $size{$move})) {
	$large ||= 'NONE'; $size{NONE} = 0;
	printf "# largest trade $large %.3f < $move %.3f, skipping\n",
	$size{$large} / $GB, $size{$move} / $GB;
	$exp{$more}{$move}++;	# skipped, keep it here
	next;
    }
    my $code = &move($more, $less, $move, '<'); # move expiring file
    $code < 0 and last;		# bail out on copy error
    $code > 0 or ++$exp{$more}{$move} and next; # keep going on file skip
    $exp{$less}{$move}++;	# record the move to new disk

    # auto-balance uneven disks by deciding if keeper trade is needed
    int($free{$less} / $GB) >= int($free{$more} / $GB) and next;

    # trade keeper file or future expire of best size to balance headroom
    my $which = '>';		# keepers first
    my $find = ($free{$more} - $free{$less}) / 2;
    my $swap = (sort { abs($size{$a} - $find) <=>
			   abs($size{$b} - $find) } keys %{$keep{$less}})[0];
    $swap or			# use younger expires if no keepers
	$swap = (sort { abs($size{$a} - $find) <=>
			    abs($size{$b} - $find) }
		 grep { $where{$_} eq $less } @exp)[0] and $which = '<';
    $swap or
	print "# no file available for trade, continuing\n"
	and next;
    $code = &move($less, $more, $swap, $which); # move swapper file
    $code < 0 and last;
    if ($which eq '>') {	# record the move
	$keep{$more}{$swap}++; delete $keep{$less}{$swap};
    } else {
	$where{$swap} = $more;
    }
    $moved{$swap} = $exp;
}
for my $dir (sort keys %exp) {	# results summary footer
    my $e = (keys %{$exp{$dir}}) - 1;
    my $k = keys %{$keep{$dir}};
    my $t = $e + $k;
    my $T = keys %size;
    printf "SUMMARY <<<%5d/%3.0f%% expires <<< + >>>%5d/%3.0f%% keepers >>>"
	. " = %5d/%3.0f%% + %.3fGB free on %s\n",
	$e, $e / $t * 100, $k, $k / $t * 100, $t, $t / $T * 100,
	$free{$dir} / $GB, $dir;
}
print $opt{go} ? "MOVED:  <<< "
    : "NOTHING DONE!  Add --go to actually move <<< ";
printf "%d = %.3f GB expires <<< and >>> %d = %.3f GB keepers >>>.\n",
    $count{'<'}, $count{'GB<'} / $GB, $count{'>'}, $count{'GB>'} / $GB;

sub move {		 # -1 = copy error, 0 = skip, 1 = copy success
    my($src, $dst, $file, $which) = @_;
    my $free = $free{$dst};
    if (($free - $size{$file}) / $GB < $MIN or
	$size{$file} > $free * $MAX) { # file too big?
	printf "# skipping %.3f GB $file, %.3f free on $dst\n",
	$size{$file} / $GB, $free / $GB;
	return 0;
    }
    $free -= $size{$file};
    printf "$which %7.3f $file: $src -> $dst %4d %7.3f $which %s\n",
    $size{$file} / $GB, scalar(keys %{$exp{$dst}}), $free / $GB, $title{$file};

    my @src = glob "$src/${file}*"; # list includes thumbnails
    my @cmd = (@copy, @src, "$dst/");
    $opt{verbose} and print "@cmd\n";
    if ($opt{go}) {		# actually do copy and unlink
	system(@cmd) == 0
	    or warn sprintf("exit %d from (@cmd)\n", $? >> 8)
	    and return -1;
	map { unlink $_ or warn "could not unlink $_: $!\n" } @src;
	$free{$src} = &free($src) * $KB; # actual disk headrooms
	$free{$dst} = &free($dst) * $KB;
    } else {			# estimated disk headrooms
	$free{$src} += $size{$file};
	$free{$dst} -= $size{$file};
    }
    $count{$which}++;
    $count{"GB$which"} += $size{$file};
    return 1;			# only if copy successful
}

sub free { return (&df(shift))[3]; } # free space of path in KB

sub df {			# df of $file in KB
    my($file) = @_;
    my @out = split "\n", `df -k $file`;
    return split /\s+/, $out[-1];
}
__END__

=head1 NAME

mythtv-balance-expire - move recording files to balance multiple disks

=head1 SYNOPSIS

 mythtv-balance-expire [--help | --man | options]
    --help	show full usage of options and exit
    --man	show the full manual page and exit
    --delta=N	expiring count delta allowed between disks [5]
    --subtitles	add subtitles to titles of moving files
    --verbose	show the commands that would be run, or are being run:
    --go	actually do the moves, else just show what would be done

=head1 DESCRIPTION

B<mythtv-balance-expire> trades recording files between multiple local
disks in a mythtv server to better balance the files over recorded
time.  Why would you want to do this you ask?

Say you have 6 months of recordings but your disk is full so you add
another.  The new disk now gets most new recordings since it has more
free space.  6 months later both are full and recording new shows.
But the original disk is now expiring files from 1 year ago while the
new disk is expiring files only 6 months old.  Mythtv's auto-expire
list implies your are losing 1 year old recordings but this is
misleading since the new disk must expire shows much younger.

Or, say you have a small disk and a large one, both full and recording
via auto-expire.  And you disable auto-expire on shows you really want
to see someday to make sure you don't lose them to auto-expire.  If
you save a significant amount of shows this way, you can eventually
have the small drive nearly full of saved files.  So the auto-expire
to record a new show is from just a few days ago, even if the large
disk is expiring shows over a year old!

So, what you really need is for all disks to hold a similar amount of
oldest expiring files.  B<This does that> by moving recordings among
the disks.

=head2 Usage and output

Run with no arguments.  This will take no actions and just show the
expiring recordings and recommended moves.

The output shows expiring recordings in time sorted order.  Lines are
indented per disk, so each "column" is a different disk.  The lines
look like:

	NNN	CHAN_YYYYMMDDHHMMSS.mpg /path/to/recording

=over 4

=item B<NNN>

Number of expiring files on this recording path

=item B<CHAN_YYYYMMDDHHMMSS.mpg>

Recording file name: channel, time stamp and file extension

=item B</path/to/recording>

Current path to the recording file

=back

Suggested moves are shown when a file should move to produce a better
balance.  These lines look like this:

	< GB expirefile /too/many/path -> /too/few/path XXX free < title
	> GB keeperfile /too/few/path -> /too/many/path YYY free > title

The expiring file is delimited by < and shown with its size in
Gigabytes.  It should move from the left path with too many to the
right path with too few as shown by the arrow.  The resulting file
count and free space of the destination location is shown.  Finally
the title (and optionally B<--subtitle>) of the moving file is
displayed.

If the destination is now more full than the source, a trade is made.
A file with auto-expire disabled (a permanent "keeper" file) is
preferred.  This keeper file is delimited by >.  It is selected by
size that would result in closest headroom on the disks.  If no
keepers are left, then a future expiring file is used for trade
instead.  When balancing to a disk with more free space, no trade is
needed.

A footer is provided which shows the end state of each disk.  A notice
is given if nothing has actually happened yet, see B<--go>.

If the suggested moves look correct, you can add B<--verbose> to see
the commands that will be run.  For the successful copies, the
original location will be removed.  If this still looks good, simply
add B<--go> to actually do the given file moves.  When using B<--go>
you must be root or the owner of the recording directories (mythtv) so
that you have permission to move the files.

=head1 OPTIONS

=over 8

=item B<--delta N>

Require each disk's expiring file count over time to be within N of
all other disks.  If a disk has > N more files than another, this
triggers a file move to maintain this delta <= N.  Lower values
produce a better balance while higher values need less moves.  You can
manipulate this value to see how many moves will be required and how
they will affect disk headroom, before deciding to use B<--go>.  A
high value like 9999 will simply show the current [im]balance with no
moves.  The default delta is 5.

=item B<--subtitles>

Append subtitles to titles of moving files.

=item B<--verbose>

Show the commands that will be run, or are being run with B<--go>.  If
STDIN is a terminal, then rsync(1) will use its B<--progress> option
to show copy progress, even without this B<--verbose>.

=item B<--go>

Actually do the file moves with rsync(1), removing the original only
if successful.  This requires write permission to the local recording
directories so you must be root or mythtv on the server.  If this
option is not given, then no actions are taken.

=back

=head1 CAVEATS

You must have ~/.mythtv/config.xml configured to read your database
and must be root or mythtv on the server for write permission to move
the files.  As shown in B<--verbose>, rsync(1) is required.

This only works with local files and may be confused by multiple
backends or remote paths.  Patches welcome if you have such a setup.
Maybe all we need to do is test that recordings exist locally.

Of course moving dozens of files will take a long time, so be patient
when using B<--go>.  Rsync(1) --progress is shown on the terminal.

This does not update the database at all.  Mythtv will automatically
discover the new locations of the recordings.  Allow time for this to
happen between B<--go> runs.

If your disks are different sizes, it may not be possible to balance
recent time.  This is normal - you will balance that time later when
you run this again in the future.  In fact, it is a good idea to run
this periodically; I do so once a month.  If you really trust the
actions, you might crontab(1) this with redirection to a log file.

=head1 COPYRIGHT

Copyright 2017-2018 Timothy D Witham.

This program is free software; you can redistribute it and/or modify
it under the same terms as Perl itself.

=head1 AUTHOR

Timothy D Witham <twitham@sbcglobal.net>

=cut
